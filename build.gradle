plugins {
    id 'com.github.johnrengelman.shadow' version '6.1.0'
    id "com.jfrog.bintray" version "1.8.4"
    id 'maven-publish'
    id 'java'
}

repositories {
    mavenCentral()
}

build.dependsOn shadowJar

version= "1.3.1"
group = "me.towdium.pinin"

def strUrl = 'https://github.com/Towdium/PinIn'
def strGit = 'git@github.com:Towdium/PinIn.git'
def strDesc = 'Java library for Chinese text match using Pinyin'
def strVer = version
def strGroup = group

def pomConfig = {
    licenses {
        license {
            name "MIT License"
            url "https://opensource.org/licenses/MIT"
            distribution "repo"
        }
    }
    developers {
        developer {
            id "towdium"
            name "Juntong Liu"
            email "towdium@gmail.com"
        }
    }
    scm {
        url strUrl
    }
}

shadowJar {
    exclude 'it/unimi/dsi/fastutil/shorts/**'
    exclude 'it/unimi/dsi/fastutil/longs/**'
    exclude 'it/unimi/dsi/fastutil/floats/**'
    exclude 'it/unimi/dsi/fastutil/doubles/**'
    exclude 'it/unimi/dsi/fastutil/bytes/**'
    exclude 'it/unimi/dsi/fastutil/booleans/**'
    exclude 'it/unimi/dsi/fastutil/ints/*Map*'
    exclude 'it/unimi/dsi/fastutil/objects/*List*'
    exclude 'it/unimi/dsi/fastutil/objects/*2Short*Map*'
    exclude 'it/unimi/dsi/fastutil/objects/*2Reference*Map*'
    exclude 'it/unimi/dsi/fastutil/objects/*2Long*Map*'
    exclude 'it/unimi/dsi/fastutil/objects/*2Int*Map*'
    exclude 'it/unimi/dsi/fastutil/objects/*2Float*Map*'
    exclude 'it/unimi/dsi/fastutil/objects/*2Double*Map*'
    exclude 'it/unimi/dsi/fastutil/objects/*2Char*Map*'
    exclude 'it/unimi/dsi/fastutil/objects/*2Byte*Map*'
    exclude 'it/unimi/dsi/fastutil/objects/*2Boolean*Map*'
    exclude 'it/unimi/dsi/fastutil/chars/*2Short*Map*'
    exclude 'it/unimi/dsi/fastutil/chars/*2Reference*Map*'
    exclude 'it/unimi/dsi/fastutil/chars/*2Long*Map*'
    exclude 'it/unimi/dsi/fastutil/chars/*2Int*Map*'
    exclude 'it/unimi/dsi/fastutil/chars/*2Float*Map*'
    exclude 'it/unimi/dsi/fastutil/chars/*2Double*Map*'
    exclude 'it/unimi/dsi/fastutil/chars/*2Char*Map*'
    exclude 'it/unimi/dsi/fastutil/chars/*2Byte*Map*'
    exclude 'it/unimi/dsi/fastutil/chars/*2Boolean*Map*'
    relocate 'it.unimi.dsi.fastutil', 'me.towdium.pinin.fastutil'
}

test {
    useJUnitPlatform()
    dependsOn cleanTest
    testLogging.showStandardStreams = true
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

dependencies {
    compile 'it.unimi.dsi:fastutil:8.3.0'
    testCompile 'org.junit.jupiter:junit-jupiter-api:5.4.0'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.4.0'
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    classifier "source"
}

publishing {
    publications {
        Upload(MavenPublication) {
            from components.java
            groupId strGroup
            artifact sourcesJar
            artifactId 'pinin'
            version strVer
            pom.withXml {
                def root = asNode()
                root.appendNode('description', strDesc)
                root.appendNode('name', 'pinin')
                root.appendNode('url', strUrl)
                root.children().last() + pomConfig
            }
        }
    }
}

artifacts {
    archives sourcesJar
}

compileJava   {
    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'
}

bintray {
    publish = false
    user = 'towdium'
    key = project.findProperty("bintray.key")
    pkg {
        repo = 'pinin'
        name = 'pinin'
        userOrg = 'towdium'
        licenses = ['MIT']
        websiteUrl = strUrl
        vcsUrl = strGit
        version {
            name = strVer
            desc = strDesc
            released  = new Date()
            vcsTag = strVer
        }
    }
    publications = ['Upload']
}
